%package com.hellblazer.pinkie.buffer.fsmExample
%import com.hellblazer.pinkie.buffer.BufferProtocol
%import java.nio.ByteBuffer
%class SimpleProtocol
%start Simple::Initial

%map Simple
%%
Initial {
    accepted(bufferProtocol: BufferProtocol)
        Connected/push(SimpleServer::ConnectionAccepted) {setBufferProtocol(bufferProtocol);}
        
    connected(bufferProtocol: BufferProtocol) 
        Connected/push(SimpleClient::Connect) {setBufferProtocol(bufferProtocol);}
}

Connected {
    protocolError
        ProtocolError {}
    closing
        Closed {}
    readError
        Closed {}
    writeError
        Closed {}
}

Closed {
}

ProtocolError {
}

Default { 
     
    closing()
        ProtocolError {}
        
    readError()
        Closed {}
        
    writeError()
        Closed {}
        
    readReady()
        ProtocolError {}
        
    writeReady()
        ProtocolError {}
}
%%

%map SimpleClient
%%

Connect 
Entry {
    establishClientSession();
}
{
    readReady
        SessionEstablished {}
}

SessionEstablished
Entry {
	ackReceived();
}	
 {
    ackReceived     SendMessage{}
    protocolError   pop(protocolError){}
}

SendMessage 
Entry {
    enableSend();
}
{
    transmitMessage(message: String) nil{ transmitMessage(message); }
    readReady AckMessage {}
    close SendGoodbye{}
}

SendGoodbye 
Entry {
    sendGoodbye();
}
{
    closing pop(closing){}
}

AckMessage
Entry {
    ackReceived();
}
{
    ackReceived     SendMessage{}
    protocolError pop(protocolError){}
}

Default {
    accepted(bufferProtocol: BufferProtocol)
        pop(protocolError) {}
     
    closing()
        pop(closing) {}
        
    connected(bufferProtocol: BufferProtocol)
        pop(protocolError) {}
        
    readError()
        pop(readError) {}
        
    writeError()
        pop(writeError) {}
        
    readReady()
        pop(protocolError) {}
        
    writeReady()
        pop(protocolError) {}
}
%%

%map SimpleServer
%%

ConnectionAccepted 
Entry {
    awaitSession();
}
{
    readReady   SessionEstablished {}
}

SessionEstablished
Entry {
    establishServerSession();
}
{
    writeReady  AwaitMessage {}
}

ProcessMessage
Entry {
    processMessage();
}
{
    messageProcessed    AwaitMessage {}
    goodBye     pop(closing){}
}

AwaitMessage
Entry {
    awaitMessage();
}
{
    readReady   ProcessMessage {}
    
}

ProtocolError {
}

Default {
    accepted(bufferProtocol: BufferProtocol)
        pop(protocolError) {}
     
    closing()
        pop(closing) {}
        
    connected(bufferProtocol: BufferProtocol)
        pop(protocolError) {}
        
    readError()
        pop(readError) {}
        
    writeError()
        pop(writeError) {}
        
    readReady()
        pop(protocolError) {}
        
    writeReady()
        pop(protocolError) {}
}
%%
